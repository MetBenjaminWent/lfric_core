!-------------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Algorithm to apply the orography field to the multigrid meshes.

module mg_orography_alg_mod

  use constants_mod,                 only: i_def, str_def
  use extrusion_mod,                 only: TWOD
  use field_mod,                     only: field_type
  use fs_continuity_mod,             only: W3
  use function_space_collection_mod, only: function_space_collection
  use inventory_by_mesh_mod,         only: inventory_by_mesh_type
  use mesh_collection_mod,           only: mesh_collection
  use mesh_mod,                      only: mesh_type


  implicit none

contains

  !> @brief Assigns the orography field to the multigrid meshes
  !> @details This algorithm first computes the surface altitude field on
  !>          each multigrid mesh by restricting the surface altitude field
  !>          from the finest mesh. Then the orography field for the
  !>          multigrid meshes is assigned from the respective surface
  !>          altitude field for each multigrid mesh.
  !> @param[in]     multigrid_mesh_names  An array of the names of the meshes
  !!                                      in the multigrid chain.
  !> @param[in,out] chi_inventory         Contains the model's coordinate fields
  !!                                      paired with their mesh.
  !> @param[in]     panel_id_inventory    Contains the model's panel ID fields
  !!                                      paired with their mesh.
  !> @param[in]     surface_altitude      Surface altitude on the finest mesh.
  subroutine mg_orography_alg( multigrid_mesh_names, &
                               chi_inventory,        &
                               panel_id_inventory,   &
                               surface_altitude       )

    use assign_orography_field_mod, only: assign_orography_field
    use restrict_scalar_unweighted_kernel_mod, &
                                    only: restrict_scalar_unweighted_kernel_type


    implicit none

    character(str_def),            intent(in)    :: multigrid_mesh_names(:)
    type(inventory_by_mesh_type),  intent(inout) :: chi_inventory
    type(inventory_by_mesh_type),  intent(in)    :: panel_id_inventory
    type(field_type),              intent(in)    :: surface_altitude

    ! local variables
    type(mesh_type),  pointer     :: mesh => null()
    type(mesh_type),  pointer     :: twod_mesh => null()
    integer(kind=i_def)           :: level, surface_order
    integer(kind=i_def)           :: num_meshes
    type(field_type), allocatable :: surface_altitude_mg(:)

    num_meshes = SIZE(multigrid_mesh_names)

    allocate(surface_altitude_mg(num_meshes))
    call surface_altitude%copy_field_properties(surface_altitude_mg(1))
    call invoke( setval_X(surface_altitude_mg(1), surface_altitude) )

    surface_order = surface_altitude%get_element_order()

    do level = 2, num_meshes

      mesh      => mesh_collection%get_mesh(multigrid_mesh_names(level))
      twod_mesh => mesh_collection%get_mesh(mesh, TWOD)

      !------------------------------------------------------------------------!
      ! Create the surface altitude fields on the multigrid meshes
      !------------------------------------------------------------------------!

      call surface_altitude_mg( level )%initialise( vector_space = &
         function_space_collection%get_fs( twod_mesh, surface_order, W3) )
      ! We use a simple restriction from fine to coarse levels
      call invoke( restrict_scalar_unweighted_kernel_type(                &
                                         surface_altitude_mg( level ),    &
                                         surface_altitude_mg( level-1 ) ) )


      !------------------------------------------------------------------------!
      ! Use the surface altitude to introduce orography on the multigrid meshes
      !------------------------------------------------------------------------!

      call assign_orography_field(chi_inventory, panel_id_inventory, mesh, &
                                  surface_altitude_mg(level))
    end do

    deallocate(surface_altitude_mg)
    nullify(mesh, twod_mesh)


  end subroutine mg_orography_alg

end module mg_orography_alg_mod
