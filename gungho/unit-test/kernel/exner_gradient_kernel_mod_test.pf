!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the exner gradient computation
!>
module exner_gradient_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use pFUnit_Mod
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: exner_gradient_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: exner_gradient_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type exner_gradient_test_type

  real(r_def), parameter :: gravity  = 10.0_r_def
  real(r_def), parameter :: radius   = 6000000_r_def
  real(r_def), parameter :: omega    = 8.0E-5_r_def
  real(r_def), parameter :: p_zero   = 100000.0_r_def
  real(r_def), parameter :: rd       = 300.0_r_def
  real(r_def), parameter :: cp       = 1000.0_r_def
  real(r_def), parameter :: scaling  = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use feign_config_mod,          only : feign_planet_config

    implicit none

    class(exner_gradient_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(exner_gradient_test_type), intent(inout) :: this

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  @Test( npes=[1] )
  subroutine test_all( this )

    use exner_gradient_kernel_mod, only : exner_gradient_code

    use get_unit_test_m3x3_q3x3x3_sizes_mod, only: get_w2_m3x3_q3x3x3_size,    &
                                                   get_w3_m3x3_q3x3x3_size,    &
                                                   get_wtheta_m3x3_q3x3x3_size
    use get_unit_test_m3x3_dofmap_mod, only: get_w2_m3x3_dofmap,            &
                                             get_w3_m3x3_dofmap,            &
                                             get_wtheta_m3x3_dofmap
    use get_unit_test_q3x3x3_basis_mod, only : get_w2_q3x3x3_diff_basis, &
                                               get_w2_q3x3x3_basis, &
                                               get_w3_q3x3x3_basis, &
                                               get_wtheta_q3x3x3_diff_basis, &
                                               get_wtheta_q3x3x3_basis
    use get_unit_test_q3x3x3_quadrature_mod, only : get_gaussian_q3x3x3_quadrature_weights_xy, &
                                                    get_gaussian_q3x3x3_quadrature_weights_z
    implicit none

    class(exner_gradient_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-8_r_def
    real(r_def), parameter :: answer = 0.0_r_def

    integer(i_def) :: cell

    integer(i_def)           :: nlayers, k, nqp_h, nqp_v
    integer(i_def)           :: ndf_w2, ndf_w3, ndf_wtheta
    integer(i_def)           :: undf_w2, undf_w3, undf_wtheta

    ! Dummy variable for passing into multi-getter routines where item is not needed
    integer(i_def) :: unused

    integer(i_def), allocatable :: map_w2(:,:)
    integer(i_def), allocatable :: map_w3(:,:)
    integer(i_def), allocatable :: map_wtheta(:,:)

    real(r_def), allocatable :: basis_w2(:,:,:,:), diff_basis_w2(:,:,:,:)
    real(r_def), allocatable :: basis_w3(:,:,:,:)
    real(r_def), allocatable :: basis_wtheta(:,:,:,:), diff_basis_wtheta(:,:,:,:)

    real(r_def), allocatable :: wh(:), wv(:)

    ! Test field data
    real(r_def), allocatable :: r_u_data(:)
    real(r_def), allocatable :: exner_data(:)
    real(r_def), allocatable :: theta_data(:)

    ! Get canned data for a 3-layer mesh
    nlayers = 3

    ! Get sizes
    call get_w2_m3x3_q3x3x3_size( ndf_w2, undf_w2, unused, &
                                  unused, unused,          &
                                  nqp_h, nqp_v,            &
                                  nlayers )

    call get_w3_m3x3_q3x3x3_size( ndf_w3, undf_w3, unused, &
                                  unused, unused,          &
                                  unused, unused,          &
                                  nlayers )

    call get_wtheta_m3x3_q3x3x3_size( ndf_wtheta, undf_wtheta, unused, &
                                      unused, unused,          &
                                      unused, unused,          &
                                      nlayers )
    ! Get dofmaps
    call get_w2_m3x3_dofmap(map_w2)
    call get_w3_m3x3_dofmap(map_w3)
    call get_wtheta_m3x3_dofmap(map_wtheta)

    call get_w2_q3x3x3_basis(basis_w2)
    call get_w2_q3x3x3_diff_basis(diff_basis_w2)
    call get_w3_q3x3x3_basis(basis_w3)
    call get_wtheta_q3x3x3_basis(basis_wtheta)
    call get_wtheta_q3x3x3_diff_basis(diff_basis_wtheta)
    call get_gaussian_q3x3x3_quadrature_weights_xy(wh)
    call get_gaussian_q3x3x3_quadrature_weights_z(wv)

    ! Create the data arrays
    allocate(r_u_data(undf_w2))
    allocate(exner_data(undf_w3))
    allocate(theta_data(undf_wtheta))

    cell = 1

    ! Create the data
    r_u_data(:) = 0.0_r_def
    exner_data(:) = 0.8_r_def
    theta_data(:) = 300.0_r_def

    call exner_gradient_code( nlayers,                                     &
                              r_u_data,                                    &
                              exner_data,                                  &
                              theta_data,                                  &
                              ndf_w2, undf_w2, map_w2(:,cell),             &
                              basis_w2, diff_basis_w2,                     &
                              ndf_w3, undf_w3, map_w3(:,cell),             &
                              basis_w3,                                    &
                              ndf_wtheta, undf_wtheta, map_wtheta(:,cell), &
                              basis_wtheta, diff_basis_wtheta,             &
                              nqp_h, nqp_v, wh, wv                         &
                            )

    k = 1
    @assertEqual(answer, r_u_data(map_w2(5, cell)+k), tol)
    @assertEqual(answer, r_u_data(map_w2(6, cell)+k), tol)

    deallocate(map_w2)
    deallocate(map_w3)
    deallocate(map_wtheta)
    deallocate(diff_basis_w2)
    deallocate(basis_w2)
    deallocate(basis_w3)
    deallocate(diff_basis_wtheta)
    deallocate(basis_wtheta)
    deallocate(wh)
    deallocate(wv)
    deallocate(theta_data)
    deallocate(exner_data)
    deallocate(r_u_data)

  end subroutine test_all

end module exner_gradient_kernel_mod_test
