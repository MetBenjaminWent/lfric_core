!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the global mesh collection object
!>
module global_mesh_collection_mod_test

  use constants_mod,                  only: i_def, IMDI, l_def, &
                                            str_max_filename, str_def
  use global_mesh_mod,                only: global_mesh_type
  use global_mesh_map_mod,            only: global_mesh_map_type
  use global_mesh_map_collection_mod, only: global_mesh_map_collection_type
  use global_mesh_collection_mod,     only: global_mesh_collection_type, &
                                            global_mesh_collection

  use yaxt,    only: xt_initialize, xt_finalize
  use mpi_mod, only: store_comm, clear_comm
  use pFUnit_Mod

  implicit none

  private

  public :: setUp, tearDown,                         &
            test_cubesphere,                         &
            test_biperiodic,                         &
            test_initial_global_mesh_map_collection, &
            test_multigrid,                          &
            test_single_file_multigrid


  @TestCase
  type, extends(MPITestCase), public :: global_mesh_collection_test_type
    private
    type(global_mesh_type) :: CubedSphere
    type(global_mesh_type) :: BiPeriodic
    type(global_mesh_type) :: TestGlobalMesh

  contains
    procedure :: setUp
    procedure :: tearDown
    procedure :: test_cubesphere
    procedure :: test_biperiodic
    procedure :: test_initial_global_mesh_map_collection
    procedure :: test_multigrid
    procedure :: test_single_file_multigrid

  end type global_mesh_collection_test_type

  character(str_def), parameter :: mesh_name = 'unit_test'
  character(str_max_filename), parameter :: &
      CubedSphereFile = 'data/mesh_C4.nc'
  character(str_max_filename), parameter :: &
      BiPeriodicFile  = 'data/mesh_BiP8x8-6000x2000.nc'

  character(str_max_filename), parameter :: &
      MultiGridFiles(4) =                   &
                  [ 'data/mesh_C4.nc ',     &! 4x4   panel
                    'data/mesh_C8.nc ',     &! 8x8   panel
                    'data/mesh_C16.nc',     &! 16x16 panel
                    'data/mesh_C32.nc' ]     ! 32x32 panel
contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(global_mesh_collection_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    ! Create top level collections
    global_mesh_collection = global_mesh_collection_type()

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod, only: final_configuration

    implicit none

    class(global_mesh_collection_test_type), intent(inout) :: this

    call global_mesh_collection%clear()

    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test( npes=[1] )
  subroutine test_cubesphere ( this )

    implicit none

    class(global_mesh_collection_test_type), intent(inout) :: this

    type(global_mesh_type), pointer :: global_mesh => null()
    character(str_max_filename)     :: filename

    integer (i_def) :: global_mesh_id
    integer (i_def) :: global_mesh_duplicate_id
    integer (i_def) :: npanels

    npanels  = 6
    filename = CubedSphereFile

    global_mesh_id =  global_mesh_collection %          &
                        add_new_global_mesh( filename,  &
                                             mesh_name, &
                                             npanels )
    global_mesh    => global_mesh_collection % &
                        get_global_mesh( global_mesh_id )

    ! Test that something is returned
    @assertAssociated ( global_mesh )

    ! Test that it has the right id
    @assertTrue ( global_mesh%get_id() == global_mesh_id )

  end subroutine test_cubesphere

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test( npes=[1] )
  subroutine test_biperiodic ( this )

    implicit none

    class(global_mesh_collection_test_type), intent(inout) :: this

    type(global_mesh_type), pointer :: global_mesh => null()
    character(str_max_filename)     :: filename

    integer (i_def) :: global_mesh_id
    integer (i_def) :: global_mesh_duplicate_id
    integer (i_def) :: npanels

    npanels  = 1
    filename = BiPeriodicFile

    global_mesh_id =  global_mesh_collection %          &
                        add_new_global_mesh( filename,  &
                                             mesh_name, &
                                             npanels )
    global_mesh    => global_mesh_collection % &
                        get_global_mesh( global_mesh_id )

    ! Test that something is returned
    @assertAssociated ( global_mesh )

    ! Test that it has the right id
    @assertTrue ( global_mesh%get_id() == global_mesh_id )

  end subroutine test_biperiodic

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test( npes=[1] )
  subroutine test_initial_global_mesh_map_collection ( this )

    implicit none

    class(global_mesh_collection_test_type), intent(inout) :: this

    type(global_mesh_type), pointer :: global_mesh => null()

    integer (i_def) :: global_mesh_id
    integer (i_def) :: global_mesh_duplicate_id
    integer (i_def) :: npanels

    ! Create a mesh collection
!    global_mesh_collection = global_mesh_collection_type()

    ! Add the Unit Test Global Mesh
    global_mesh_id =  global_mesh_collection % add_unit_test_global_mesh()
    global_mesh    => global_mesh_collection % get_global_mesh( global_mesh_id )

    ! Test that something is returned
    @assertAssociated ( global_mesh )

    ! Test that it has the right id
    @assertTrue ( global_mesh%get_id() == global_mesh_id )

  end subroutine test_initial_global_mesh_map_collection

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test( npes=[1] )
  subroutine test_multigrid ( this )

    implicit none

    class(global_mesh_collection_test_type), intent(inout) :: this

    type(global_mesh_type), pointer :: global_mesh => null()
    type(global_mesh_type), pointer :: MultiGrid1  => null()
    type(global_mesh_type), pointer :: MultiGrid2  => null()
    type(global_mesh_type), pointer :: MultiGrid3  => null()
    type(global_mesh_type), pointer :: MultiGrid4  => null()


    type(global_mesh_type), pointer :: tmp_global_mesh  => null()

    type(global_mesh_map_type), pointer :: &
                                       global_mesh_map => null()

    integer (i_def) :: MultiGrid_id(4)

    integer (i_def) :: tmp_int, i
    integer (i_def) :: npanels

    integer(i_def), allocatable :: test_global_id_map(:,:)

    ! Create a mesh collection
    global_mesh_collection = global_mesh_collection_type()

    npanels = 6

    do i=1, 4
      MultiGrid_id(i) = global_mesh_collection %                  &
                          add_new_global_mesh( MultiGridFiles(i), &
                                               mesh_name,         &
                                               npanels )
    end do

    MultiGrid1 => global_mesh_collection%get_global_mesh( MultiGrid_id(1) )
    MultiGrid2 => global_mesh_collection%get_global_mesh( MultiGrid_id(2) )
    MultiGrid3 => global_mesh_collection%get_global_mesh( MultiGrid_id(3) )
    MultiGrid4 => global_mesh_collection%get_global_mesh( MultiGrid_id(4) )

    ! Test ids are different
    @assertTrue ( MultiGrid_id(1) /= MultiGrid_id(2) )
    @assertTrue ( MultiGrid_id(2) /= MultiGrid_id(3) )
    @assertTrue ( MultiGrid_id(3) /= MultiGrid_id(4) )

    !===========================================================================
    ! Test the number of maps assigned to each global mesh
    ! Multigrid1  mesh should have 1 maps
    ! Multigrid2  mesh should have 2 maps
    ! Multigrid3  mesh should have 2 maps
    ! Multigrid4  mesh should have 1 maps
    !===========================================================================
    ! MultiGrid 1
    global_mesh     => global_mesh_collection % get_global_mesh(MultiGrid_id(1))
    global_mesh_map => global_mesh % get_global_mesh_map(MultiGrid_id(2))
    @assertAssociated( global_mesh_map )


    ! MultiGrid 2
    global_mesh     => global_mesh_collection % get_global_mesh(MultiGrid_id(2))
    global_mesh_map => global_mesh % get_global_mesh_map(MultiGrid_id(1))
    @assertAssociated( global_mesh_map )
    global_mesh_map => global_mesh % get_global_mesh_map(MultiGrid_id(3))
    @assertAssociated( global_mesh_map )

    ! MultiGrid 3
    global_mesh     => global_mesh_collection % get_global_mesh(MultiGrid_id(3))
    global_mesh_map => global_mesh % get_global_mesh_map(MultiGrid_id(2))
    @assertAssociated( global_mesh_map )
    global_mesh_map => global_mesh % get_global_mesh_map(MultiGrid_id(4))
    @assertAssociated( global_mesh_map )

    ! MultiGrid 4
    global_mesh     => global_mesh_collection % get_global_mesh(MultiGrid_id(4))
    global_mesh_map => global_mesh % get_global_mesh_map(MultiGrid_id(3))
    @assertAssociated( global_mesh_map )


    ! Check the maps
    !==============================================================================
    ! 96 cell --> 384 cell
    global_mesh     => global_mesh_collection % get_global_mesh(MultiGrid_id(1))
    global_mesh_map => global_mesh % get_global_mesh_map(MultiGrid_id(2))

    tmp_int = global_mesh_map % get_source_id()
    @assertEqual( tmp_int, MultiGrid_id(1) )

    tmp_int = global_mesh_map % get_target_id()
    @assertEqual( tmp_int, MultiGrid_id(2) )

    tmp_int = global_mesh_map % get_nsource_cells()
    @assertEqual( 96, tmp_int )

    tmp_int = global_mesh_map % get_ntarget_cells_per_source_cell()
    @assertEqual( 4, tmp_int )


    ! Check gid map
    allocate(test_global_id_map(4,1))

    call global_mesh_map % get_cell_map([1], test_global_id_map )  ! On panel 1
    @assertEqual( [1,2,9,10], test_global_id_map(:,1) )

    call global_mesh_map % get_cell_map([16], test_global_id_map ) ! On panel 1
    @assertEqual( [55,56,63,64], test_global_id_map(:,1)  )

    call global_mesh_map % get_cell_map([40], test_global_id_map ) ! On panel 3
    @assertEqual( [151,152,159,160], test_global_id_map(:,1)  )

    call global_mesh_map % get_cell_map([70], test_global_id_map ) ! On panel 5
    @assertEqual( [275,276,283,284], test_global_id_map(:,1)  )

    deallocate(test_global_id_map)

    ! 384 cell --> 96 cell
    global_mesh     => global_mesh_collection % get_global_mesh(MultiGrid_id(2))
    global_mesh_map => global_mesh % get_global_mesh_map(MultiGrid_id(1))

    tmp_int = global_mesh_map%get_source_id()
    @assertEqual( tmp_int, MultiGrid_id(2) )

    tmp_int = global_mesh_map%get_target_id()
    @assertEqual( tmp_int, MultiGrid_id(1) )

    tmp_int = global_mesh_map%get_nsource_cells()
    @assertEqual( 384, tmp_int )

    tmp_int = global_mesh_map%get_ntarget_cells_per_source_cell()
    @assertEqual( 1, tmp_int )


    ! Check gid map
    allocate(test_global_id_map(1,1))

    call global_mesh_map%get_cell_map([1], test_global_id_map )   ! On panel 1
    @assertEqual( [1], test_global_id_map(:,1) )

    call global_mesh_map%get_cell_map([55], test_global_id_map )  ! On panel 1
    @assertEqual( [16], test_global_id_map(:,1)  )

    call global_mesh_map%get_cell_map([160], test_global_id_map ) ! On panel 3
    @assertEqual( [40], test_global_id_map(:,1)  )

    call global_mesh_map%get_cell_map([283], test_global_id_map ) ! On panel 5
    @assertEqual( [70], test_global_id_map(:,1)  )

    deallocate(test_global_id_map)


    ! 384 cell --> 1536 cell
    global_mesh     => global_mesh_collection % get_global_mesh(MultiGrid_id(2))
    global_mesh_map => global_mesh % get_global_mesh_map(MultiGrid_id(3))

    tmp_int = global_mesh_map%get_source_id()
    @assertEqual( tmp_int, MultiGrid_id(2) )

    tmp_int = global_mesh_map%get_target_id()
    @assertEqual( tmp_int, MultiGrid_id(3) )

    tmp_int = global_mesh_map%get_nsource_cells()
    @assertEqual( 384, tmp_int )

    tmp_int = global_mesh_map%get_ntarget_cells_per_source_cell()
    @assertEqual( 4,  tmp_int )

    ! Check gid map
    allocate(test_global_id_map(4,1))

    call global_mesh_map%get_cell_map([1], test_global_id_map )    ! On panel 1
    @assertEqual( [1,2,17,18], test_global_id_map(:,1) )

    call global_mesh_map%get_cell_map([120], test_global_id_map )  ! On panel 2
    @assertEqual( [463,464,479,480], test_global_id_map(:,1)  )

    call global_mesh_map%get_cell_map([170], test_global_id_map )  ! On panel 3
    @assertEqual( [675,676,691,692], test_global_id_map(:,1)  )

    call global_mesh_map%get_cell_map([287], test_global_id_map )  ! On panel 5
    @assertEqual( [1133,1134,1149,1150], test_global_id_map(:,1)  )

    deallocate(test_global_id_map)

  end subroutine test_multigrid


  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test( npes=[1] )
  subroutine test_single_file_multigrid ( this )

    use feign_config_mod,  only: feign_development_config
    use configuration_mod, only: final_configuration
    use ncdf_quad_mod,     only: ncdf_quad_type

    use development_config_mod,  only: implement_consolidated_multigrid, &
                                       single_mesh_input

    implicit none

    class(global_mesh_collection_test_type), intent(inout) :: this

    type(global_mesh_type), pointer :: global_mesh        => null()
    type(global_mesh_type), pointer :: target_mesh        => null()
    type(global_mesh_type), pointer :: test_global_mesh_A => null()
    type(global_mesh_type), pointer :: test_global_mesh_B => null()

    character(str_def), parameter :: &
        MultiGrid_mesh_names(4) = ['C4 ','C8 ','C16','C32' ]

    character(str_def), parameter :: &
        another_chain(2) = ['C4 ','C12' ]

    character(str_def), parameter :: &
        all_meshes(5) = ['C4 ','C8 ','C16','C32','C12' ]

    character(str_def), allocatable :: target_mesh_names(:)
    character(str_def), allocatable :: test_character_array(:)
    character(str_def) :: mesh_name
    character(str_def) :: target_mesh_name
    character(str_def) :: test_character

    type(global_mesh_map_type), pointer :: global_mesh_map => null()

    logical(l_def) :: test_log = .true.

    integer(i_def), allocatable :: mesh_map(:,:)
    integer(i_def), allocatable :: test_global_id_map(:,:)
    integer(i_def) :: multigrid_ids(4)
    integer(i_def) :: another_chain_ids(2)
    integer(i_def) :: test_integer_A
    integer(i_def) :: test_integer_B

    integer (i_def) :: i, j
    integer (i_def) :: test_integer
    integer (i_def) :: kgo_integer

    integer (i_def) :: n_meshes
    integer (i_def) :: npanels = 6

    type(ncdf_quad_type) :: file_handler

    ! This unit-test is for new features which should not be accessed until
    ! implementation. Set namelist testing to true so the new code can be
    ! tested
    call feign_development_config(                              &
             implement_consolidated_multigrid = test_log,       &
             single_mesh_input='data/mesh_C32_MG.nc',           &
             spawn_2d_mesh=.false., spawn_shifted_mesh=.false., &
             spawn_double_level_mesh=.false. )

    ! First read all the requested global meshes into memory.
    ! This is done because the Intergrid map ids are based
    ! on the global mesh ids. So all the required global mesh
    ! topologies must be read in first.
    do i=1, size(multigrid_mesh_names)

      multiGrid_ids(i) = global_mesh_collection %                          &
                             add_new_global_mesh( single_mesh_input,       &
                                                  multigrid_mesh_names(i), &
                                                  npanels )
    end do

    do i=1, size(another_chain)
      another_chain_ids(i) = global_mesh_collection %                &
                             add_new_global_mesh( single_mesh_input, &
                                                  another_chain(i),  &
                                                  npanels )
    end do

    ! Check that collection returns correct number of meshes read in.
    !=================================================================
    test_integer_A = global_mesh_collection % n_meshes()

    ! Total meshes should be 5 as C4 is repeated in both
    ! multigrid_mesh_names and another_chain
    test_integer_B = size(all_meshes)
    @assertTrue( test_integer_A == test_integer_B )

    ! Check the names of the meshes
    !=================================================================
    test_character_array = global_mesh_collection % get_mesh_names()
    @assertTrue( test_character_array == all_meshes )

    ! Check that requesting global mesh by id returns the same as when
    ! requesting it by name.
    !=================================================================
    test_global_mesh_A => global_mesh_collection % get_global_mesh( multigrid_mesh_names(2) )
    test_global_mesh_B => global_mesh_collection % get_global_mesh( multigrid_ids(2) )
    @assertEqual( test_global_mesh_A%get_id(),test_global_mesh_B%get_id() )

    ! Test the query routine
    !=================================================================
    do i=1, 4
      @assertTrue( global_mesh_collection%check_for(multigrid_mesh_names(i)) )
    end do

    test_character ='bad mesh'
    @assertFalse( global_mesh_collection%check_for(test_character) )


    ! Test ids are different
    !=================================================================
    @assertTrue ( multigrid_ids(1) /= multigrid_ids(2) )
    @assertTrue ( multigrid_ids(2) /= multigrid_ids(3) )
    @assertTrue ( multigrid_ids(3) /= multigrid_ids(4) )

    !-----------------------------------------------
    ! Now test that global mesh maps are read in
    ! correctly to a preloaded global mesh object.
    !-----------------------------------------------
    ! Should really be in a unit-test for ncdf_quad_mod
    ! though the exisitng functionality was tested here.
    ! May consider moving this out at a later date
    !-----------------------------------------------

    ! Read in the maps for each global mesh
    !=================================================================
    call file_handler%file_open(trim(single_mesh_input))
    do i=1, size(multigrid_mesh_names)
      global_mesh => global_mesh_collection%get_global_mesh( multigrid_mesh_names(i) )
      call global_mesh%get_target_mesh_names(target_mesh_names)
      do j=1, size(target_mesh_names)
        if (global_mesh_collection%check_for(target_mesh_names(j))) then
          call file_handler%read_map( multigrid_mesh_names(i), &
                                      target_mesh_names(j),    &
                                      mesh_map )
          target_mesh => global_mesh_collection%get_global_mesh( target_mesh_names(j) )
          call global_mesh%add_global_mesh_map(target_mesh, mesh_map)
        end if
      end do
    end do
    call file_handler%file_close()

    !=================================================================
    ! Get the meshes from the collection so we can test
    ! the number of maps assigned to each global mesh.
    !=================================================================
    ! Mesh C4  should have 2 maps
    ! Mesh C8  should have 2 maps
    ! Mesh C16 should have 2 maps
    ! Mesh C32 should have 1 maps
    !=================================================================
    ! MultiGrid C4:C8, C4:C12
    mesh_name   = 'C4'
    global_mesh => global_mesh_collection%get_global_mesh( mesh_name )

    target_mesh_name = 'C8'
    target_mesh      => global_mesh_collection%get_global_mesh( target_mesh_name )
    global_mesh_map  => global_mesh%get_global_mesh_map( target_mesh%get_id() )
    @assertAssociated( global_mesh_map )
    @assertEqual( global_mesh%get_id(), global_mesh_map%get_source_id() )
    @assertEqual( target_mesh%get_id(), global_mesh_map%get_target_id() )

    target_mesh_name = 'C12'
    target_mesh      => global_mesh_collection%get_global_mesh( target_mesh_name )
    global_mesh_map  => global_mesh%get_global_mesh_map( target_mesh%get_id() )
    @assertAssociated( global_mesh_map )
    @assertEqual( global_mesh%get_id(), global_mesh_map%get_source_id() )
    @assertEqual( target_mesh%get_id(), global_mesh_map%get_target_id() )


    ! MultiGrid C4:C8:C16
    mesh_name   = 'C8'
    global_mesh => global_mesh_collection % get_global_mesh( mesh_name )

    target_mesh_name = 'C4'
    target_mesh      => global_mesh_collection % get_global_mesh( target_mesh_name )
    global_mesh_map  => global_mesh % get_global_mesh_map( target_mesh%get_id() )
    @assertAssociated( global_mesh_map )
    @assertEqual( global_mesh%get_id(), global_mesh_map%get_source_id() )
    @assertEqual( target_mesh%get_id(), global_mesh_map%get_target_id() )

    target_mesh_name = 'C16'
    target_mesh      => global_mesh_collection % get_global_mesh( target_mesh_name )
    global_mesh_map  => global_mesh % get_global_mesh_map( target_mesh%get_id() )
    @assertAssociated( global_mesh_map )
    @assertEqual( global_mesh%get_id(), global_mesh_map%get_source_id() )
    @assertEqual( target_mesh%get_id(), global_mesh_map%get_target_id() )


    ! MultiGrid C8:C16:C32
    mesh_name   = 'C16'
    global_mesh => global_mesh_collection % get_global_mesh( mesh_name )

    target_mesh_name = 'C8'
    target_mesh      => global_mesh_collection % get_global_mesh( target_mesh_name )
    global_mesh_map  => global_mesh % get_global_mesh_map( target_mesh%get_id() )
    @assertAssociated( global_mesh_map )
    @assertEqual( global_mesh%get_id(), global_mesh_map%get_source_id() )
    @assertEqual( target_mesh%get_id(), global_mesh_map%get_target_id() )

    target_mesh_name = 'C32'
    target_mesh      => global_mesh_collection % get_global_mesh( target_mesh_name )
    global_mesh_map  => global_mesh % get_global_mesh_map( target_mesh%get_id() )
    @assertAssociated( global_mesh_map )
    @assertEqual( global_mesh%get_id(), global_mesh_map%get_source_id() )
    @assertEqual( target_mesh%get_id(), global_mesh_map%get_target_id() )


    ! MultiGrid C16:C32
    mesh_name   = 'C32'
    global_mesh => global_mesh_collection % get_global_mesh( mesh_name )

    target_mesh_name = 'C16'
    target_mesh      => global_mesh_collection % get_global_mesh( target_mesh_name )
    global_mesh_map  => global_mesh % get_global_mesh_map( target_mesh%get_id() )

    @assertAssociated( global_mesh_map )
    @assertEqual( global_mesh%get_id(), global_mesh_map%get_source_id() )
    @assertEqual( target_mesh%get_id(), global_mesh_map%get_target_id() )

    !==============================================================================
    ! CHECK THE MAPS
    !==============================================================================
    ! 96 cell (C4) --> 384 cell (C8)
    !==============================================================================
    mesh_name = 'C4'
    target_mesh_name = 'C8'
    global_mesh     => global_mesh_collection % get_global_mesh(mesh_name)
    target_mesh     => global_mesh_collection % get_global_mesh(target_mesh_name)
    global_mesh_map => global_mesh % get_global_mesh_map(target_mesh%get_id())

    test_integer = global_mesh_map % get_source_id()
    @assertEqual( test_integer, global_mesh%get_id() )

    test_integer = global_mesh_map % get_target_id()
    @assertEqual( test_integer, target_mesh%get_id() )

    test_integer = global_mesh_map % get_nsource_cells()
    @assertEqual( global_mesh%get_ncells(), test_integer )

    test_integer = global_mesh_map % get_ntarget_cells_per_source_cell()
    kgo_integer  = max( target_mesh%get_ncells()/global_mesh%get_ncells(),1 )
    @assertEqual( kgo_integer, test_integer )

    ! Check global ID map
    allocate(test_global_id_map(global_mesh_map % get_ntarget_cells_per_source_cell(),1))

    call global_mesh_map % get_cell_map([1], test_global_id_map )  ! On panel 1
    @assertEqual( [1,2,9,10], test_global_id_map(:,1) )

    call global_mesh_map % get_cell_map([16], test_global_id_map ) ! On panel 1
    @assertEqual( [55,56,63,64], test_global_id_map(:,1)  )

    call global_mesh_map % get_cell_map([40], test_global_id_map ) ! On panel 3
    @assertEqual( [151,152,159,160], test_global_id_map(:,1)  )

    call global_mesh_map % get_cell_map([70], test_global_id_map ) ! On panel 5
    @assertEqual( [275,276,283,284], test_global_id_map(:,1)  )

    deallocate(test_global_id_map)

    !==============================================================================
    ! 96 cell (C4) --> 10368 cell (C12)
    !==============================================================================
    mesh_name = 'C4'
    target_mesh_name = 'C12'
    global_mesh     => global_mesh_collection % get_global_mesh(mesh_name)
    target_mesh     => global_mesh_collection % get_global_mesh(target_mesh_name)
    global_mesh_map => global_mesh % get_global_mesh_map(target_mesh%get_id())

    test_integer = global_mesh_map % get_source_id()
    @assertEqual( test_integer, global_mesh%get_id() )

    test_integer = global_mesh_map % get_target_id()
    @assertEqual( test_integer, target_mesh%get_id() )

    test_integer = global_mesh_map % get_nsource_cells()
    @assertEqual( global_mesh%get_ncells(), test_integer )

    test_integer = global_mesh_map % get_ntarget_cells_per_source_cell()
    kgo_integer  = max( target_mesh%get_ncells()/global_mesh%get_ncells(),1 )
    @assertEqual( kgo_integer, test_integer )

    ! Check global ID map
    allocate(test_global_id_map(global_mesh_map % get_ntarget_cells_per_source_cell(),1))

    call global_mesh_map % get_cell_map([1], test_global_id_map )  ! On panel 1
    @assertEqual( [1,2,3,13,14,15,25,26,27], test_global_id_map(:,1) )

    call global_mesh_map % get_cell_map([16], test_global_id_map ) ! On panel 1
    @assertEqual( [118,119,120,130,131,132,142,143,144], test_global_id_map(:,1) )

    call global_mesh_map % get_cell_map([40], test_global_id_map ) ! On panel 3
    @assertEqual( [334,335,336,346,347,348,358,359,360], test_global_id_map(:,1) )

    call global_mesh_map % get_cell_map([70], test_global_id_map ) ! On panel 5
    @assertEqual( [616,617,618,628,629,630,640,641,642], test_global_id_map(:,1)  )

    deallocate(test_global_id_map)


    !==============================================================================
    ! 384 cell (C8) --> 96 cell (C4)
    !==============================================================================
    mesh_name = 'C8'
    target_mesh_name = 'C4'
    global_mesh     => global_mesh_collection % get_global_mesh(mesh_name)
    target_mesh     => global_mesh_collection % get_global_mesh(target_mesh_name)
    global_mesh_map => global_mesh % get_global_mesh_map(target_mesh%get_id())


    test_integer = global_mesh_map%get_source_id()
    @assertEqual( test_integer, global_mesh%get_id() )

    test_integer = global_mesh_map%get_target_id()
    @assertEqual( test_integer, target_mesh%get_id() )

    test_integer = global_mesh_map%get_nsource_cells()
    @assertEqual( global_mesh%get_ncells(), test_integer )

    test_integer = global_mesh_map%get_ntarget_cells_per_source_cell()
    kgo_integer  = max( target_mesh%get_ncells()/global_mesh%get_ncells(),1 )
    @assertEqual( kgo_integer, test_integer )

    ! Check global ID map
    allocate(test_global_id_map(global_mesh_map%get_ntarget_cells_per_source_cell(),1))

    call global_mesh_map%get_cell_map([1], test_global_id_map )   ! On panel 1
    @assertEqual( [1], test_global_id_map(:,1) )

    call global_mesh_map%get_cell_map([55], test_global_id_map )  ! On panel 1
    @assertEqual( [16], test_global_id_map(:,1)  )

    call global_mesh_map%get_cell_map([160], test_global_id_map ) ! On panel 3
    @assertEqual( [40], test_global_id_map(:,1)  )

    call global_mesh_map%get_cell_map([283], test_global_id_map ) ! On panel 5
    @assertEqual( [70], test_global_id_map(:,1)  )

    deallocate(test_global_id_map)


    !==============================================================================
    ! 384 cell (C8) --> 1536 cell (C16)
    !==============================================================================
    mesh_name = 'C8'
    target_mesh_name = 'C16'
    global_mesh     => global_mesh_collection % get_global_mesh(mesh_name)
    target_mesh     => global_mesh_collection % get_global_mesh(target_mesh_name)
    global_mesh_map => global_mesh % get_global_mesh_map(target_mesh%get_id())


    test_integer = global_mesh_map%get_source_id()
    @assertEqual( test_integer, global_mesh%get_id() )

    test_integer = global_mesh_map%get_target_id()
    @assertEqual( test_integer, target_mesh%get_id() )

    test_integer = global_mesh_map%get_nsource_cells()
    @assertEqual( global_mesh%get_ncells(), test_integer )

    test_integer = global_mesh_map%get_ntarget_cells_per_source_cell()
    kgo_integer  = max( target_mesh%get_ncells()/global_mesh%get_ncells(),1 )
    @assertEqual( kgo_integer, test_integer )

    ! Check global ID map
    allocate(test_global_id_map(global_mesh_map%get_ntarget_cells_per_source_cell(),1))

    call global_mesh_map%get_cell_map([1], test_global_id_map )    ! On panel 1
    @assertEqual( [1,2,17,18], test_global_id_map(:,1) )

    call global_mesh_map%get_cell_map([120], test_global_id_map )  ! On panel 2
    @assertEqual( [463,464,479,480], test_global_id_map(:,1)  )

    call global_mesh_map%get_cell_map([170], test_global_id_map )  ! On panel 3
    @assertEqual( [675,676,691,692], test_global_id_map(:,1)  )

    call global_mesh_map%get_cell_map([287], test_global_id_map )  ! On panel 5
    @assertEqual( [1133,1134,1149,1150], test_global_id_map(:,1)  )

    deallocate(test_global_id_map)

  end subroutine test_single_file_multigrid

end module global_mesh_collection_mod_test
